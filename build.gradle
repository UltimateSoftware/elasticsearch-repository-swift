// Workaround as per https://github.com/elastic/elasticsearch/issues/49787#issuecomment-562720655
import org.elasticsearch.gradle.testclusters.TestClustersRegistry
import org.elasticsearch.gradle.testclusters.TestClustersPlugin


buildscript {
    ext {
        elasticsearchVersion = '7.5.2'
    }

    repositories {
        mavenLocal()
        jcenter()
    }
    dependencies {
        classpath "org.elasticsearch.gradle:build-tools:$elasticsearchVersion"
    }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'elasticsearch.esplugin'

java {
    targetCompatibility = JavaVersion.VERSION_1_8
    sourceCompatibility = JavaVersion.VERSION_1_8
}

idea {
    targetCompatibility = JavaVersion.VERSION_1_8
    sourceCompatibility = JavaVersion.VERSION_1_8
}

ext {
    commonsCodecVersion = '1.11'
    commonsioVersion = '2.6'
    commonslangVersion = '2.6'
    commonsloggingVersion = '1.1.3'
    jacksonmapperVersion = '1.9.13'
    guavaVersion = '23.0'
    httpclientVersion = '4.5.10'
    httpcoreVersion = '4.4.12'
    javaxActivationVersion = '1.1'
    junitVersion = '4.12'
    jossVersion = '0.10.4'
    slf4jVersion = '1.7.26'
}

group = 'org.wikimedia.elasticsearch.swift'
version = '3.0.0-es' + elasticsearchVersion
description = """OpenStack Swift repository plugin for Elasticsearch"""

esplugin {
    name 'repository-swift'
    version rootProject.version
    description 'OpenStack Swift repository plugin for Elasticsearch'
    classname 'org.wikimedia.elasticsearch.swift.SwiftRepositoryPlugin'
    licenseFile rootProject.file('LICENSE.txt')
    noticeFile rootProject.file('NOTICE.txt')
}

repositories {
    jcenter()
}
dependencies {
    compileOnly group: 'org.elasticsearch', name: 'elasticsearch', version: elasticsearchVersion
    compile group: 'com.google.guava', name: 'guava', version: guavaVersion
    compile group: 'org.javaswift', name: 'joss', version: jossVersion
    compile group: 'org.apache.httpcomponents', name: 'httpclient', version: httpclientVersion
    compile group: 'commons-codec', name: 'commons-codec', version: commonsCodecVersion
    compile group: 'commons-lang', name: 'commons-lang', version: commonslangVersion
    compile group: 'commons-io', name: 'commons-io', version: commonsioVersion
    runtime group: 'org.apache.httpcomponents', name: 'httpcore', version: httpcoreVersion
    runtime group: 'javax.activation', name: 'activation', version: javaxActivationVersion
    runtime group: 'org.slf4j', name: 'slf4j-api', version: slf4jVersion
    runtime group: 'org.slf4j', name: 'slf4j-log4j12', version: slf4jVersion
    runtime group: 'commons-logging', name: 'commons-logging', version: commonsloggingVersion
    runtime group: 'org.codehaus.jackson', name: 'jackson-mapper-asl', version: jacksonmapperVersion
    testCompile group: 'junit', name: 'junit', version: junitVersion
    testCompile group: 'org.elasticsearch.test', name: 'framework', version: elasticsearchVersion
}

dependencyLicenses.enabled = false
thirdPartyAudit.enabled = false

TestClustersRegistry registry = project.rootProject.extensions.create("testClustersRegistry", TestClustersRegistry)
TestClustersPlugin.configureClaimClustersHook(project.gradle, registry)
TestClustersPlugin.configureStartClustersHook(project.gradle, registry)
TestClustersPlugin.configureStopClustersHook(project.gradle, registry)